def leaseMod := /.at.lang.leasedrefs;
import /.at.lang.futures;
import /.at.lang.totam;

def log(text) {
  Android.Log.i("weKittens", "\n\n#####\n"+ text + "\n#####");
};

deftype remotePlayer;

def makeWeKittens(myUserId := /.at.support.util.randomNumberBetween(0,125)) {

  def otherPlayers := [];// stores remote players.
  def otherPlayersIds := []; // stores other players ids
  def playerName := "EMPTY PLAYER NAME";
  def playerId;

  def game; // current game state
  def tuples; // tuple space, useful to keep track of cards played during the game
  def currentRound;

  def gui;

  //interface for android gui.
  def localInterface := object: {
    def ping(newgui){
        log("GUI HAS ARRIVED");
        gui := newgui;
    };

    def addNewPlayer(name){
        log("NEW PLAYER HAS ARRIVED "+name);
        playerName := name;
        goOnline();
    };

    def getPlayerName(){
        playerName;
    };

    def startGame(){
        playerId := 0;
        currentRound := -1;
        game := jlobby.edu.vub.at.wekittens.GameLogic.new(playerId, otherPlayers.length+1, myUserId, otherPlayersIds);
        def deck := game.getDeckList();
        def playersCards := game.getPlayersCards();
        def playersStates := game.getPlayersStates();
        def nbPlayers := game.getNbPlayers();
        def playerIdsList := game.getListPlayerdsIds();
        // start the tuple space if game starter
        tuples := makeTupleSpace();
        def otherPlayerId := 0;
        otherPlayers.each:{ |player|
            otherPlayerId := otherPlayerId + 1;
            player<-startGame(otherPlayerId, deck, playersCards, playersStates, nbPlayers, playerIdsList); // send needed information to players
        };
        tuples.goOnline(); // tuples go online
        waitForTuples();
    };

    def sendTuple(cardId, from, to, currentRound, deck, states){
        def t := tuple: ["Message", cardId, from, to, currentRound, deck, states];
        log(t);
        tuples.inject: t; // inject public tuple
    };
  };

    // wait for tuples to be received
  def waitForTuples(){
        def tu := tuple: ["Message", var: `cardId, var: `from, var: `to, var: `round, var: `deck, var: `states];
        def waitForIncomings := tuples.whenever: tu read:{ // non blocking reading
            log("currentRound: "+currentRound);
            log("round: "+round);
            if: ((from != playerId)) then: { // do nothing if we read our own tuple and
                if: (round != currentRound) then: { //we do not read an old tuple
                    currentRound := round;
                    gui.handleTuple(cardId, from, to, round, deck, states);
                };
            } else:{
                currentRound := round;
            };
        };
        waitForIncomings;
  };

  //interface for remote painters.
  def remoteInterface := object: {

    def getPlayerName(){
        playerName;
    };

    def getMyUserId(){
        myUserId;
    };

    def startGame(id,deck,playersCards,playerStates,nbPlayers,playerIdsList){
        tuples := makeTupleSpace();
        tuples.goOnline();
        playerId := id;
        currentRound := -1;
        gui.startGameAT(id,deck,playersCards,playerStates,nbPlayers,playerIdsList);
        waitForTuples();
    };

    def getTuple(cardId, from, to){

    };

  }; //end-remoteInterface


  def goOnline() {
  	// setup peer-to-peer service discovery
  	log("Going online, waiting for new players to connect");

  	whenever: remotePlayer discovered: { |player| //when a new player is discovered
  	    if: !otherPlayers.contains(player) then: { //if new player
  	        log("New player found: " + player);
            when: player<-getPlayerName()@FutureMessage becomes:{|name| //wait to retrieve player's name
                when: player<-getMyUserId()@FutureMessage becomes:{|id|
  	                otherPlayers := otherPlayers + [player]; // when we have the player
                    otherPlayersIds := otherPlayersIds + [id]; // save their ids
                    gui.foundNewPlayer(name); // only when we have the far reference + id call the gui (to keep list order)
                    // TODO OK juste trop de deco / reco wifi pourri
                    //whenever: player disconnected: {
                    //    if: (id != myUserId) then: {
                    //        log("Player "+ id + " disconnected !");
                    //        gui.playerDisconnected(id);
                    //        when: player<-getMyUserId()@Due(30.seconds) becomes: {|i| // player has 30 seconds to come back
                    //            log("Player "+i+" is back !");
                    //        } catch: TimeoutException using:{ |e|
                    //            log("Player "+id+" will never come back, removing him from game");
                    //            gui.removePlayerFromGame(id);
                    //        };
                    //    };
                    //};

                    whenever: player reconnected: {
                        if: (id != myUserId) then: {
                            log("Reconnected player "+id);
                        };
                    };
                };
            };
  	    };
  	};


  	export: remoteInterface as: remotePlayer; //export ATInterface object
  };

  network.online();
  log("Hallo Android!!"); //?
  //jlobby.edu.vub.at.wekittens.GameLogic.test();

  // example symbiosis
  //Android.parent.topExplosionAnimation();

  gui := Android.parent.registerATApp(localInterface); //import gui for modifications
};

//network.online;
self;